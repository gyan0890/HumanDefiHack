include "List.aes"
contract TicTacToe =

    //The state will store the ID of the ongoing game, the details of the game 
    //and the details of the players in the system
    record state = {
        numGame: int,
        playerId: address,
        gameDetails: map(int, game),
        playerDetails: map(address, player) }

    record game = {
        gameId: int,
        player1Id: address,
        player2Id: address,
        game_moves: map(int,int), //Will store the values of the playerMove once they make a move on a particular square
        gameCompleted: bool,
        gameWinner: address}

    record player = {
        playerId: address,
        playerName: string,
        numGamesWon: int }
    
    stateful entrypoint init() = {
        numGame = 1,
        playerId = Contract.creator,
        gameDetails = {},
        playerDetails = {} }
    
    public stateful entrypoint register_player(name : string, wallet_player: address) : player =
        let new_player: player = {
            playerName = name,
            playerId = wallet_player,
            numGamesWon = 0} //playerMove will be assigned once the player is assigned to a Game
        put(state{playerId = wallet_player})
        put(state{playerDetails[new_player.playerId] = new_player})
        
        state.playerDetails[new_player.playerId]

    
    // //Initialise a game - This means a game is open for players to join
    public stateful entrypoint initialisegame(): game =
        let new_game : game = {
            gameId = state.numGame,
            player1Id = Contract.creator,
            player2Id = Contract.creator,
            game_moves = {},
            gameWinner = Contract.creator,
            gameCompleted = false }
        put(state{gameDetails[new_game.gameId] = new_game})
        put(state{numGame = state.numGame+1})

        state.gameDetails[new_game.gameId]
    
    public stateful entrypoint assignPlayertoGame(gameId: int, player1: address, player2: address): game =
        // game_players 
        
        require(Map.member(player1, state.playerDetails), "Player not registered")
        require(Map.member(player2, state.playerDetails), "Player not registered")

        //Updating the global playerDetails
        put(state{gameDetails[gameId].player1Id = player1})
        put(state{gameDetails[gameId].player2Id = player2})
        
        state.gameDetails[gameId]
    
    public stateful entrypoint makeMove(gameId:int, playerId:address, square: int): game =
        //Check if the player exists in the database
        require(Map.member(playerId, state.playerDetails), "Player not registered")
        //Check to ensure that the chosen square number is between 1-9
        require(square < 10 && square > 0, "Square value should be between 1-9")
        //Checks if the square selected is already occupied
        require(!Map.member(square, state.gameDetails[gameId].game_moves), "Square occupied")
        //Checks if the given game is completed or not
        require(!state.gameDetails[gameId].gameCompleted, "Cannot make amove in a completed game")
        //TODO: Add condition to check if player is one of the players in the given game
        require(((playerId == state.gameDetails[gameId].player1Id) || (playerId == state.gameDetails[gameId].player2Id)), "Player is not registered in this game")

        if(playerId == state.gameDetails[gameId].player1Id)
            put(state{gameDetails[gameId].game_moves[square] = 1})  
        else
            put(state{gameDetails[gameId].game_moves[square] = 2})
        
        state.gameDetails[gameId]

    public stateful entrypoint gameCheck(gameId: int, square1: int, square2:int, square3:int): bool =
        let moves = state.gameDetails[gameId].game_moves
        if(Map.member(square1, moves) && Map.member(square2, moves) && Map.member(square3, moves))
            if(moves[square1] == moves[square2] && moves[square2] == moves[square3])
                if(moves[square1] == 1)
                    put(state{gameDetails[gameId].gameWinner = state.gameDetails[gameId].player1Id})
                    put(state{gameDetails[gameId].gameCompleted = true})
                    true
                else
                    put(state{gameDetails[gameId].gameWinner = state.gameDetails[gameId].player2Id})
                    put(state{gameDetails[gameId].gameCompleted = true})
                    true
            else
                false
        else
            false
                

    public stateful entrypoint boardCheck(gameId: int): string = 
        if(gameCheck(gameId, 1,2,3) || gameCheck(gameId, 1, 4, 7) || gameCheck(gameId, 1, 5, 9) || gameCheck(gameId, 4, 5, 6) || gameCheck(gameId, 8, 9, 7) || gameCheck(gameId, 2, 5, 8) || gameCheck(gameId, 3, 6, 9) || gameCheck(gameId, 3, 5, 7))
            state.playerDetails[state.gameDetails[gameId].gameWinner].playerName
        else
            "In Progress"

    
    public stateful entrypoint returnGameDetails(gameId: int): bool =
        Map.member(gameId, state.gameDetails)

    public stateful entrypoint returnPlayerDetails(playerId: address): bool =
        Map.member(playerId, state.playerDetails)
